plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'org.jetbrains.kotlin.kapt' version '1.4.32'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id 'application'
}

group = 'me.ceparpet'
version = '1.0-SNAPSHOT'

// for gradle run task
mainClassName = 'TournamentAppKt'
run {
    args = ['server', 'config.yaml']
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testImplementation 'io.mockk:mockk:1.10.6'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'org.jeasy:easy-random-core:5.0.0'
    testImplementation 'io.dropwizard:dropwizard-testing:2.0.21'
    testCompile "org.testcontainers:testcontainers:1.15.3"
    testCompile "org.testcontainers:junit-jupiter:1.15.3"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'

    implementation 'io.dropwizard:dropwizard-core:2.0.21'
    implementation 'io.dropwizard:dropwizard-assets:2.0.21'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'io.dropwizard.modules:dropwizard-redis:1.5.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.34.1'
    kapt 'com.google.dagger:dagger-compiler:2.34.1'
    implementation 'com.google.dagger:dagger:2.34.1'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.12.+"
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '11'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '11'
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': 'TournamentAppKt'
    }
    archiveName 'tournament-betclic-dropwizard.jar'
}